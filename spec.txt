pattern -> spaces "(" spaces "?<" spaces name spaces ">" spaces expr spaces ")" spaces 
         | spaces expr spaces

name    -> [a-zA-Z_0-9]+

expr    -> "{" spaces "}"
         | "{" spaces pair spaces (',' spaces pair)* spaces "}"
         | "[" spaces "]"
         | "[" spaces "*" spaces "]"
         | "[" spaces span spaces "]"
         | "[" spaces span spaces "," spaces "*" spaces "]"
         | "[" spaces span spaces "," spaces tail
         | "[" spaces tail
         | "<" spaces ">"
         | "<" spaces (pattern spaces (',' spaces pattern)*)? spaces ">" ("/g")?
         | "<!" spaces (pattern spaces (',' spaces pattern)*)? spaces "!>" ("/g")?
         | "_"
         | "true"
         | "false"
         | "null"
         | number
         | string
         | regex
         | "(" spaces "!<" spaces name spaces ">" spaces type spaces ")"

pair    -> string spaces ":" spaces pattern
         | string spaces ":" spaces "_"
         | "_" spaces ":" spaces pattern

span    -> pattern (spaces "," spaces pattern)*

tail    -> span_find (spaces "," spaces span_find)* spaces ("," spaces "*" spaces)? "]"

span_find -> "*" spaces "," spaces span

number  -> integer decimal frac
         | integer decimal
         | integer frac
         | integer
integer  -> [+-]? digit+
decimal  -> "." digit+
frac     -> [+-]? [eE] digit+

regex   -> "#" spaces string
string  -> "\"" char* "\""
type    -> "string"
         | "number"
         | "boolean"
         | "regex"

char    -> "\\u" hexa hexa hexa hexa
         | "\\n"
         | "\\r"
         | "\\t"
         | "\\b"
         | "\\f"
         | "\\s"
         | "\\\\"
         | "\\\""
         | !("\"" | "\\") valid_cp
valid_cp -> .

spaces  -> space*
space   -> [ \n\r\t]
